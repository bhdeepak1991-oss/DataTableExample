ALTER PROCEDURE SP_GetEmployees_DataTable
    @SearchValue NVARCHAR(200) = '',
    @FirstNameSearch NVARCHAR(200) = '',
    @MiddleNameSearch NVARCHAR(200) = '',
    @LastNameSearch NVARCHAR(200) = '',
    @EmailIdSearch NVARCHAR(200) = '',
    @SortColumn NVARCHAR(50) = 'Id',
    @SortDirection NVARCHAR(5) = 'asc',
    @Start INT = 0,
    @Length INT = 10
AS
BEGIN
    SET NOCOUNT ON;
    Declare @TotalCount INT=0;
    Declare @FilteredCount INT =0;

    -- Total count (without filters)
    SELECT @TotalCount = COUNT(1)
    FROM Employee;

    -- Filtered count
    SELECT @FilteredCount = COUNT(1)
    FROM Employee
    WHERE
        ( @SearchValue = '' OR
          FirstName LIKE '%' + @SearchValue + '%' OR
          MiddleName LIKE '%' + @SearchValue + '%' OR
          LastName LIKE '%' + @SearchValue + '%' OR
          EmailId LIKE '%' + @SearchValue + '%' )
        AND ( @FirstNameSearch = '' OR FirstName LIKE '%' + @FirstNameSearch + '%' )
        AND ( @MiddleNameSearch = '' OR MiddleName LIKE '%' + @MiddleNameSearch + '%' )
        AND ( @LastNameSearch = '' OR LastName LIKE '%' + @LastNameSearch + '%' )
        AND ( @EmailIdSearch = '' OR EmailId LIKE '%' + @EmailIdSearch + '%' );

    -- Paged Data
    SELECT  @TotalCount AS TotalCount,
            @FilteredCount AS FilteredCount,
            Id, FirstName, MiddleName, LastName, EmailId
    FROM Employee
    WHERE
        ( @SearchValue = '' OR
          FirstName LIKE '%' + @SearchValue + '%' OR
          MiddleName LIKE '%' + @SearchValue + '%' OR
          LastName LIKE '%' + @SearchValue + '%' OR
          EmailId LIKE '%' + @SearchValue + '%' )
        AND ( @FirstNameSearch = '' OR FirstName LIKE '%' + @FirstNameSearch + '%' )
        AND ( @MiddleNameSearch = '' OR MiddleName LIKE '%' + @MiddleNameSearch + '%' )
        AND ( @LastNameSearch = '' OR LastName LIKE '%' + @LastNameSearch + '%' )
        AND ( @EmailIdSearch = '' OR EmailId LIKE '%' + @EmailIdSearch + '%' )
    ORDER BY
        CASE WHEN @SortColumn='Id' AND @SortDirection='asc' THEN Id END ASC,
        CASE WHEN @SortColumn='Id' AND @SortDirection='desc' THEN Id END DESC,

        CASE WHEN @SortColumn='FirstName' AND @SortDirection='asc' THEN FirstName END ASC,
        CASE WHEN @SortColumn='FirstName' AND @SortDirection='desc' THEN FirstName END DESC,

        CASE WHEN @SortColumn='MiddleName' AND @SortDirection='asc' THEN MiddleName END ASC,
        CASE WHEN @SortColumn='MiddleName' AND @SortDirection='desc' THEN MiddleName END DESC,

        CASE WHEN @SortColumn='LastName' AND @SortDirection='asc' THEN LastName END ASC,
        CASE WHEN @SortColumn='LastName' AND @SortDirection='desc' THEN LastName END DESC,

        CASE WHEN @SortColumn='EmailId' AND @SortDirection='asc' THEN EmailId END ASC,
        CASE WHEN @SortColumn='EmailId' AND @SortDirection='desc' THEN EmailId END DESC

    OFFSET @Start ROWS FETCH NEXT @Length ROWS ONLY;
END
